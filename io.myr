use std

pkg io = 
	type status = union
		`Ok
		`Err byte[:]
		`Eof
	;;

	trait readable @a =
		read : (r : @a, b : byte[:] -> (uint64, status))
	;;
	
	trait writeable @a =
		write : (w : @a, b : byte[:] -> (uint64, status))
	;;
	
	generic copy : (w : @a::writeable, r : @b::readable ->  (uint64, status)) 
	generic fill : (r : @a::readable, buf : byte[:] ->  (uint64, status))
	generic writeall : (w : @a::writeable, buf : byte[:] ->  (uint64, status))
	generic slurp : (r : @a::readable ->  std.result(byte[:], byte[:]))

	type nullwriter = void

	impl writeable nullwriter =
		write = {w, buf; -> (buf.len, `Ok)}
	;;
	
	var discard : nullwriter
;;

generic copy = {w, r
	var buf : byte[4096]
	var ncopied
	var rn, wn
	var rstatus, wstatus
	
	ncopied = 0
	while true
		(rn, rstatus) = fill(r, buf[:])
		(wn, wstatus) = writeall(w, buf[:rn])
		ncopied += wn
		match rstatus
		| `Ok:
		| `Eof: -> (ncopied, `Ok)
		| `Err msg: -> (ncopied, `Err msg)
		;;
		match wstatus
		| `Ok:
		| `Eof: -> (ncopied, `Ok)
		| `Err msg: -> (ncopied, `Err msg)
		;;
	;;
}

generic fill = {r, buf
	var nread
	var n, status
	
	nread = 0
	while buf.len != 0
		(n, status) = read(r, buf)
		nread += n
		buf = buf[n:]
		match status
		| `Ok:
		| `Eof: -> (nread, `Eof)
		| `Err msg: -> (nread, `Err msg)
		;;
	;;
	-> (nread, `Ok)
}

generic writeall = {w, buf
	var nwrite
	var n, status
	
	nwrite = 0
	while buf.len != 0
		(n, status) = write(w, buf)
		nwrite += n
		buf = buf[n:]
		match status
		| `Ok:
		| `Eof: -> (nwrite, `Eof)
		| `Err msg: -> (nwrite, `Err msg)
		;;
	;;
	-> (nwrite, `Ok)
}


generic slurp = {r
	var len, bufsz
	var buf
	var n, status

	len = 0
	bufsz = 4096
	buf = std.slalloc(bufsz)
	while true
		(n, status) = read(r, buf[len:])
		len += n
		match status
		| `Ok:
			if len + 2*n >= bufsz castto(uint64)
				bufsz *= 2
				std.slgrow(&buf, bufsz)
			;;
		| `Eof:
			-> `std.Ok buf[:len]
		| `Err msg:
			std.slfree(buf)
			-> `std.Fail msg
		;;
	;;
	std.die("unreachable")
}
